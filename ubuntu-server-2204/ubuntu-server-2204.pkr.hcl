# Template for Ubuntu Server 22.04

variable "proxmox_api_url" {
    type = string
}

variable "proxmox_api_token_id" {
    type = string
    sensitive = true
}

variable "proxmox_api_token_secret" {
    type = string
    sensitive = true
}

source "proxmox" "ubuntu-server-2204" {
    # Connection settings
    proxmox_url = "${var.proxmox_api_url}"
    username = "${var.proxmox_api_token_id}"
    token = "${var.proxmox_api_token_secret}"
    node = "pve"

    iso_file = "local:iso/ubuntu-22.04-live-server-amd64.iso"
    iso_storage_pool = "local"
    # Alternatively, the ISO could be downloaded at the time of the build.
    # iso_url = "https://releases.ubuntu.com/22.04/ubuntu-22.04-live-server-amd64.iso"
    # This is a SHA256 hash
    iso_checksum = "84AEAF7823C8C61BAA0AE862D0A06B03409394800000B3235854A6B38EB4856F"

    # Using a self signed certificate on my server. Change to false if your certifciate is trusted.
    insecure_skip_tls_verify = true

    # The default of 512 will cause a kernel panic with Subiquity. 1G should be the minimum allocated.
    memory = "2048"
    cores = "1"

    # l26 is for Linux 2.6+
    os = "l26"

    network_adapters {
        model = "virtio"
        bridge = "vmbr0"
        firewall = "false"
    }

    disks {
        storage_pool = "Datastore1"
        storage_pool_type = "directory"
        type = "scsi"
        disk_size = "20G"
        format = "qcow2"
    }

    template_name = "Ubuntu-22.04-Template"
    template_description = "Template generated by Packer"

    unmount_iso = true

    qemu_agent = true

    scsi_controller = "virtio-scsi-pci"

    cloud_init = true
    cloud_init_storage_pool = "Datastore1"

    boot_command = [
        "<esc><wait>",
        "e<wait>",
        "<down><down><down><end>",
        "<bs><bs><bs><bs><wait>",
        "autoinstall ds=nocloud-net\\;s=http://{{ .HTTPIP }}:{{ .HTTPPort }}/ ---<wait>",
        "<f10><wait>"
    ]
    boot = "c"
    boot_wait = "5s"

    http_directory = "http"

    ssh_username = "packer"
    ssh_private_key_file = "~/.ssh/packer"
    ssh_timeout = "30m"

}

build {

    name = "Ubuntu-22.04-Template"
    sources = ["source.proxmox.ubuntu-server-2204"]

    # Prep VM for cloning.
    provisioner "shell" {
        inline = [
            "while [ ! -f /var/lib/cloud/instance/boot-finished ]; do echo 'Waiting for cloud-init...'; sleep 1; done",
            "sudo rm /etc/ssh/ssh_host_*",
            "sudo truncate -s 0 /etc/machine-id",
            "sudo apt -y autoremove --purge",
            "sudo apt -y clean",
            "sudo apt -y autoclean",
            "sudo cloud-init clean",
            "sudo rm -f /etc/cloud/cloud.cfg.d/subiquity-disable-cloudinit-networking.cfg",
            "sudo sync"
        ]
    }

    provisioner "file" {
        source = "files/99-pve.cfg"
        destination = "/tmp/99-pve.cfg"
    }

    # Cloud-config
    provisioner "shell" {
        inline = [ "sudo cp /tmp/99-pve.cfg /etc/cloud/cloud.cfg.d/99-pve.cfg" ]
    }

}